apply from: 'versions.gradle'
apply from: 'deps.gradle'

buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.31"
    }
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    project.status = isReleaseBuild() ? 'release' : 'integration'
    project.group = project.property('pom.groupId')
    project.version = getVersionString()

    if (project.name != "copyrightcheck") {
        apply from: "$rootDir/gradle-fury/gradle/maven-support.gradle"
    }
}

def isReleaseBuild() {
    return isTag() && !version.contains("SNAPSHOT")
}

static boolean isTag() {
    def tag = System.getenv('TRAVIS_TAG')
    return !(tag == null || tag.isEmpty())
}

String getVersionString() {
    String buildNumber = System.getenv('TRAVIS_BUILD_NUMBER') ?: "0"
    String version = project.property('pom.version').replaceAll(".(0)\$", "." + buildNumber)
    return isTag() ? version : version + "-SNAPSHOT"
}